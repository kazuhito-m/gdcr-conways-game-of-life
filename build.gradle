buildscript {
    ext {
        springBootVersion = '2.6.3'
        encoding = 'UTF-8'
        compatibility = JavaVersion.VERSION_17
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id "com.github.node-gradle.node" version "3.1.1"
}

repositories {
    mavenCentral()
}

group = 'github.com.kazuhitom'
version = '0.0.1-SNAPSHOT'

node {
    version = '11.15.0'
    npmVersion = '6.7.2'
    download = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.1'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.2'

    implementation 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// gulp_test叩くときには、強制的に npm_install を前に実行するように。
npm_test.dependsOn npm_install

// gradle test の前には、かならず「jsのテスト」を挟む。
test.dependsOn npm_test

// test の際は、必ずJacocoでカバレッジ取るように。
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}
test.finalizedBy jacocoTestReport

// gradle bootRun の前にも、必ず「jsのトランスパイル」を挟む
processResources.dependsOn npm_run_build
